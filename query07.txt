# Find the number of US rivers that ultimately flow into a non-river water body and give a sample river for each. a river ultimately flows into a water body if the water body is not itself a river and either the river flows into it directly, or flows into another river that ultimately flows into it.
PREFIX db: <http://dbpedia.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX yago: <http://dbpedia.org/class/yago/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>

SELECT ?nonRiverBody (count(?river) as ?riverCount) (SAMPLE(?river) as ?example) WHERE
  {
  ?river rdf:type dbo:River;
         dbo:state ?state;
         dbo:riverMouth+ ?nonRiverBody.
   ?state rdf:type yago:StatesOfTheUnitedStates.
  FILTER NOT EXISTS{?nonRiverBody rdf:type dbo:River}
  }
GROUP BY ?nonRiverBody
ORDER BY DESC(?riverCount)